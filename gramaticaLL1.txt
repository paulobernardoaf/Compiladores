S           = 'int' SR
            | NotIntType Decl S
            | 'void' DeclFun S

SR          = 'Main' '(' Param ')' OpSentCl
            | Decl S

Param       = ParamList
            | EPSILON

ParamList   = ParVar ParamListR

ParVar      = VarType OptFnArray 'nameId'

VarType     = 'int'
            | 'float'
            | 'string'
            | 'bool'
            | 'char'

OptFnArray  = '[' OptFnArrayR
            | EPSILON

OptFnArrayR = ArrSize ']'
            | ']'

ArrSize     = 'nameId'
            | 'cteInt'

ParamListR  = ',' ParamList
            | EPSILON

OpSentCl    = '{' Sent '}'

Sent        = VarType OptArray DeclVar Sent
            | FunCall ';' Sent
            | Commands Sent
            | Var Atr ';' Sent
            | 'return' ReturnParam ';' Sent
            | EPSILON

OptArray    = '[' ArrSize ']'
            | EPSILON

DeclVar     = 'nameId' Atr ';'

Atr         = '=' AtrR
            | EPSILON

AtrR        = Ec
            | '[' EcList ']'

EcList      = EcListValues
            | EPSILON

EcListValues= Ec EcListR

EcListR     = ',' EcListValues
            | EPSILON

FunCall     = 'funcId' '(' ParCall ')'

ParCall     = ParCallList
            | EPSILON

ParCallList = Ec ParCalListR

ParCalListR = ',' ParCallList
            | EPSILON

Commands    = 'printComm' '(' 'cteString' PrintOpt ')' ';'
            | 'get' '(' GetVarList ')' ';'
            | 'if' Eb OpSentCl ElseComm
            | 'from' Var '=' Ea 'to' Ea 'increment' Ea OpSentCl
            | 'during' Eb OpSentCl

PrintOpt    = ',' ParCallList
            | EPSILON

GetVarList  = Var GetVarListR

Var         = 'nameId' OptArray

GetVarListR = ',' GetVarList
            | EPSILON

ElseComm    = 'else' OpSentCl
            | EPSILON

ReturnParam = Ec
            | '[' EcList ']'
            | EPSILON

NotIntType  =  'float'
            | 'string'
            | 'bool'
            | 'char'  

Decl        = '[' DeclR
            | DeclAux

DeclR       = ']' DeclFun
            | ArrSize ']' DeclAux

DeclFun     = 'funcId' '(' Param ')' OpSentCl

DeclAux     = DeclFun
            | DeclVar

Ec          = Eb Ecr

Ecr         = '&' Eb Ecr
            | EPSILON
        
Eb          = Tb Ebr
        
Ebr         = 'opOr' Tb Ebr
            | EPSILON
        
Tb          = Fb Tbr 
        
Tbr         = 'opAnd' Fb Tbr
            | EPSILON
        
Fb          = Fc Fbr            
            | 'opNot' Fb    
            | 'cteBool'
        
Fbr         = 'RelLtGt' Fc Fbr
            | EPSILON
        
Fc          = 'cteString'
            | 'cteChar'
            | Ra
        
Ra          = Ea Rar
        
Rar         = 'opEq' Ea Rar
            | EPSILON
        
Ea          = Ta Ear
        
Ear         = 'AddSub' Ta Ear
            | EPSILON
        
Ta          = Fa Tar
        
Tar         = 'Mult' Fa Tar
            | EPSILON
        
Fa          = '(' Ec ')'
            | 'UnNeg' Fa
            | Var
            | FunCall
            | 'cteInt'
            | 'CteFloat'