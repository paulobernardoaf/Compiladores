S           = VarType Decl S
            | 'void' DeclFun S
            | EPSILON

Decl        = '[' DeclR
            | DeclAux

DeclR       = ']' DeclFun
            | ArrSize ']' DeclAux

DeclAux     = DeclFun
            | DeclVar

DeclFun     = IdFun '(' Param ')' OpSentCl
 
DeclVar     = 'nameId' Atr ';'

Atr         = '=' AtrR
            | EPSILON

AtrR        = Ec
            | '[' EcList ']'

EcList      = EcListValues
            | EPSILON

EcListValues= Ec EcListR

EcListR     = ',' EcListValues
            | EPSILON

Var         = 'nameId' OptArray

IdFun       = 'funcId'
            | 'Main'

ArrSize     = 'nameId'
            | 'cteInt'

VarType     = 'int'
            | 'float'
            | 'string'
            | 'bool'
            | 'char'

OpSentCl    = '{' Sent '}'

Param       = ParamList
            | EPSILON

ParamList   = ParVar ParamListR

ParamListR  = ',' ParamList
            | EPSILON

ParVar      = VarType OptFnArray 'nameId'

OptFnArray  = '[' OptFnArrayR
            | EPSILON

OptFnArrayR = ArrSize ']'
            | ']'

Sent        = VarType OptArray DeclVar Sent
            | FunCall ';' Sent
            | Commands Sent
            | Var Atr ';' Sent
            | 'return' ReturnParam ';' Sent
            | EPSILON

OptArray    = '[' ArrSize ']'
            | EPSILON

ReturnParam = Ec
            | '[' EcList ']'
            | EPSILON

FunCall     = 'funcId' '(' ParCall ')'

ParCall     = ParCallList
            | EPSILON

ParCallList = Ec ParCalListR

ParCalListR = ',' ParCallList
            | EPSILON

Commands    = 'printComm' '(' 'cteString' PrintOpt ')' ';'
            | 'get' '(' GetVarList ')' ';'
            | 'if' Eb OpSentCl ElseComm
            | 'from' Var '=' Ea 'to' Ea 'increment' Ea OpSentCl
            | 'during' Eb OpSentCl

PrintOpt    = ',' ParCallList
            | EPSILON

GetVarList  = Var GetVarListR

GetVarListR = ',' GetVarList
            | EPSILON
ElseComm    = 'else' OpSentCl
            | EPSILON

Ec          = Eb Ecr
Ecr         = '&' Eb Ecr
            | EPSILON
        
Eb          = Tb Ebr
        
Ebr         = 'opOr' Tb Ebr
            | EPSILON
        
Tb          = Fb Tbr 
        
Tbr         = 'opAnd' Fb Tbr
            | EPSILON
        
Fb          = Fc Fbr            
            | 'opNot' Fb    
            | 'cteBool'
        
Fbr         = 'RelLtGt' Fc Fbr
            | EPSILON
        
Fc          = 'cteString'
            | 'cteChar'
            | Ra
        
Ra          = Ea Rar
        
Rar         = 'opEq' Ea Rar
            | EPSILON
        
Ea          = Ta Ear
        
Ear         = 'AddSub' Ta Ear
            | EPSILON
        
Ta          = Fa Tar
        
Tar         = 'Mult' Fa Tar
            | EPSILON
        
Fa          = '(' Ec ')'
            | 'UnNeg' Fa
            | Var
            | FunCall
            | 'cteInt'
            | 'CteFloat'