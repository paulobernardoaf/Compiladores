S           = DeclFun S
            | DeclVar S
            | 'int' Main

Main        = 'Main' OpParCl OpSentCl

DeclFun     = FuncType DeclArrOpc 'funcId' OpParCl OpSentCl

DeclVar     = VarType DeclArrObg 'nameId' ';'
            | VarType DeclArrObg 'nameId' '=' Ec ';'

Var         = 'nameId' DeclArrObg

DeclArrOpc  = '[' ArrSizeOpc ']'
            | EPSILON

DeclArrObg  = '[' ArrSizeObg ']'
            | EPSILON

ArrSizeOpc  = 'nameId'
            | 'cteInt'
            | EPSILON

ArrSizeObg  = 'nameId'
            | 'cteInt'           

VarType     = 'int'
            | 'float'
            | 'string'
            | 'bool'
            | 'char'
            
FuncType    = VarType
            | 'void'

OpParCl     = '(' Param ')'

OpSentCl    = '{' Sent '}'

ParVar      = VarType DeclArrOpc 'nameId'

Param       = ParamList
            | EPSILON

ParamList   = ParVar ',' ParamList
            | ParVar

Sent        = DeclVar Sent
            | FunCall Sent
            | Commands Sent
            | Var Atr ';' Sent
            | Return ';' Sent
            | EPSILON

Atr         = '=' Ec
            | '=' '[' EcList ']'
            | EPSILON   
            
EcList      = EcListValues
            | EPSILON

EcListValues= Ec EcListR

EcListR     = ',' EcListValues
            | EPSILON            

FunCall     = 'funcId' OpParCallCl ';'

OpParCallCl = '(' ParCall ')'

ParCall     = ParCallList
            | EPSILON

ParCallList = Ec ',' ParCallList
            | Ec

Commands    = PrintComm
            | GetComm
            | IfComm
            | FromComm
            | DuringComm

PrintComm   = 'printComm' '(' 'cteString' PrintOpt ')' ';'

PrintOpt    = ',' ParCallList
            | EPSILON

GetComm     = 'get' '(' GetVarList ')' ';'

GetVarList  = Var ',' GetVarList
            | Var

IfComm      = 'if' Eb OpSentCl ElseComm

ElseComm    = 'else' OpSentCl
            | EPSILON

FromComm    = 'from' Var '=' Ea 'to' Ea 'increment' Ea OpSentCl

DuringComm  = 'during' Eb OpSentCl

Return      = 'return' Ec 
            | 'return' '[' EcList ']'
            | 'return'

Ec          = Eb Ecr
Ecr         = '&' Eb Ecr
            | EPSILON

Eb          = Tb Ebr
Ebr         = 'opOr' Tb Ebr
            | EPSILON

Tb          = Fb Tbr 
Tbr         = 'opAnd' Fb Tbr
            | EPSILON

Fb          = Fc Fbr            
            | 'opNot' Fb    
            | 'cteBool'
Fbr         = 'RelLtGt' Fc Fbr
            | EPSILON

Fc          = 'cteString'
            | 'cteChar'
            | Ra

Ra          = Ea Rar             
Rar         = 'opEq' Ea Rar
            | EPSILON

Ea          = Ta Ear 
Ear         = 'AddSub' Ta Ear
            | EPS1ILON

Ta          = Fa Tar
Tar         = 'Mult' Fa Tar
            | EPSILON

Fa          = '(' Ec ')'
            | 'UnNeg' Fa        
            | Var
            | FunCall
            | 'cteInt'
            | 'cteFloat'